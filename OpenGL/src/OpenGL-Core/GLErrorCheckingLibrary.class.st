"
I decorate a GLLibrary to automatically check for errors after every API call.

Whenever an API call to OpenGL fails, OpenGL internally sets a flag to denote the kind of error that occurred. To retrieve this flag, users need to manually call the `glGetError` function. Since users (understandably) balk at interspersing this error check after every API call, errors can and frequently do happen without the calling code noticing anything wrong.

Instead of leaving error checking to user code, I ensure a call to `glGetError` after very API call.
Great for debugging and general sanity. Buy now.

---

Note: Since I essentially double the amount of FFI calls being made, performance can in certain use cases be diminished significantly.
"
Class {
	#name : #GLErrorCheckingLibrary,
	#superclass : #GLLazyDecoratorLibrary,
	#category : #'OpenGL-Core',
	#'squeak_changestamp' : 'stlu 4/14/2021 15:16'
}

{
	#category : #'instance creation',
	#'squeak_changestamp' : 'stlu 4/17/2021 10:29'
}
GLErrorCheckingLibrary class >> around: aLibrary [

	^ self new
		library: aLibrary;
		context: aLibrary context;
		yourself
]

{
	#category : #examples,
	#'squeak_changestamp' : 'stlu 4/19/2021 12:18'
}
GLErrorCheckingLibrary class >> example1 [ "self example1"
	"An OpenGL example which produces an internal OpenGL error.
	Uses the automatic error checking.
	Immediately throws an error after an erroneous api call was executed."
	| window context library |
	window := GLFWWindow
					extent: 400 @ 400
					title: self name , ' example1'.
	window ifNil: [^ self error: 'GLFW Error'].
	context := window context.
	
	library := context library withErrorChecking. "wraps error checking library around default library"
	[library makeCurrentDuring: [
		[window shouldClose] whileFalse: [
			GL clear: GL_COLOR. "invalid enum. should be GL_COLOR_BUFFER_BIT"
			context swapBuffers.
			GLFW pollEvents]]
	] ensure: [context destroy]
]

{
	#category : #compiling,
	#'squeak_changestamp' : 'stlu 4/16/2021 15:25'
}
GLErrorCheckingLibrary >> errorCheckingTemplate [

	^ '{1}

	| result |
	result := library {1}.
	library checkForError.
	^ result'
]

{
	#category : #api,
	#'squeak_changestamp' : 'stlu 4/16/2021 15:32'
}
GLErrorCheckingLibrary >> getError [
	"Manual passtrhough. Do not check the error checking for errors."
	^ library getError
]

{
	#category : #compiling,
	#'squeak_changestamp' : 'stlu 6/17/2021 21:02'
}
GLErrorCheckingLibrary >> install: aSelector [

	| registryMethod signature template source |
	registryMethod := GLRegistry compiledMethodAt: aSelector ifAbsent: [^ nil].
	"assumption: method signature is on a single line ended by a carriage return."
	signature := registryMethod getSource asString copyUpTo: Character cr.
	template := (registryMethod pragmaAt: #glCommand:)
					ifNil: [self passthroughTemplate]
					ifNotNil: [self errorCheckingTemplate].
	source := template format: { signature }.
	^ self class compileSilently: source classified: #'*autogenerated'
]

{
	#category : #accessing,
	#'squeak_changestamp' : 'stlu 4/15/2021 22:23'
}
GLErrorCheckingLibrary >> library [

	^ library
]

{
	#category : #accessing,
	#'squeak_changestamp' : 'stlu 4/17/2021 10:29'
}
GLErrorCheckingLibrary >> library: aLibrary [

	library := aLibrary
]
