Class {
	#name : #GLRegistryGenerator,
	#superclass : #Object,
	#instVars : [
		'generatedMethods',
		'shouldCompileSilently'
	],
	#category : #'OpenGL-Generation'
}

{
	#category : #helpers,
	#'squeak_changestamp' : 'stlu 9/20/2021 17:38'
}
GLRegistryGenerator >> compile: sourceString classified: categorySymbol into: aClass [

	| selector previousSource |
	selector := Parser new parseSelector: sourceString.
	generatedMethods add: (MethodReference class: aClass selector: selector).
	
	previousSource := (aClass sourceCodeAt: selector ifAbsent: []) asString.
	sourceString = previousSource ifTrue: [
		aClass organization classify: selector under: categorySymbol logged: false.
		^ self].
	
	shouldCompileSilently
		ifTrue: [aClass compileSilently: sourceString classified: categorySymbol]
		ifFalse: [aClass compile: sourceString classified: categorySymbol].
]

{
	#category : #initialization,
	#'squeak_changestamp' : 'stlu 9/20/2021 17:36'
}
GLRegistryGenerator >> initialize [

	generatedMethods := Set new.
	shouldCompileSilently := false.
]

{
	#category : #helpers,
	#'squeak_changestamp' : 'stlu 4/12/2021 17:03'
}
GLRegistryGenerator >> removeGeneratedMethodsIn: aClass [

	(Pragma allNamed: #generated in: aClass) do: [:pragma |
		aClass removeSelector: pragma method selector]
]

{
	#category : #helpers,
	#'squeak_changestamp' : 'stlu 4/12/2021 17:43'
}
GLRegistryGenerator >> removeNonGeneratedMethodsIn: aClass [
	"Remove methods in target class that were not generated by this generator."
	aClass selectors do: [:selector |
		| reference |
		reference := MethodReference class: aClass selector: selector.
		(generatedMethods includes: reference) ifFalse: [
			aClass removeSelector: selector]]
]

{
	#category : #accessing,
	#'squeak_changestamp' : 'stlu 9/20/2021 17:36'
}
GLRegistryGenerator >> shouldCompileSilently [

	^ shouldCompileSilently
]

{
	#category : #accessing,
	#'squeak_changestamp' : 'stlu 9/20/2021 17:36'
}
GLRegistryGenerator >> shouldCompileSilently: anObject [

	shouldCompileSilently := anObject.
]
